@echo on && setlocal ENABLEEXTENSIONS

set "PLATFORM=x64"
set "OKIBA_BRANCH=develop"

set "ROOT=%~dp0"
for %%a in ("%ROOT%\.") do set "ROOT=%%~fa"
pushd "%ROOT%"

if not defined INSTALLER_URL (
  set "INSTALLER_URL=https://raw.githubusercontent.com/neguse11/cm3d2_plugins_okiba/%OKIBA_BRANCH%/scripts/installer.bat"
)
set "INSTALLER_FILE=installer.bat"
set "INSTALLER_TMP=%INSTALLER_FILE%.tmp"

powershell -Command "(New-Object Net.WebClient).DownloadFile('%INSTALLER_URL%', '%INSTALLER_TMP%')"
if not exist "%INSTALLER_TMP%" (
  echo インストーラーのダウンロードに失敗しました
  exit /b 1
)
type "%INSTALLER_TMP%" | more /p > "%INSTALLER_FILE%"
del "%INSTALLER_TMP%"

call "%INSTALLER_FILE%"
del "%INSTALLER_FILE%"
popd

if defined OKIBA_DONT_SELF_DELETE ( exit /b 0 )
(goto) 2>nul & del "%~f0"
